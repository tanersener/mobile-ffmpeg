apply plugin: 'com.android.application'

android {
    signingConfigs {
        localConfig {
            keyAlias 'androiddebugkey'
            storeFile file(System.getProperty("user.home") + '/.android/debug.keystore')
            storePassword 'android'
            keyPassword 'android'
        }
    }
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.arthenica.mobileffmpeg.test"
        minSdkVersion 24
        targetSdkVersion 28
        versionCode 240421
        versionName "4.2.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    splits {
        abi {
            enable true
            reset()
            include "armeabi-v7a", "arm64-v8a", "x86", "x86_64"
            universalApk false
        }
    }
}

ext.abiCodes = ['x86':1, 'x86_64':2, 'armeabi-v7a':3, 'arm64-v8a':4]

import com.android.build.OutputFile

android.applicationVariants.all { variant ->
    variant.outputs.each {
        output ->
            def baseAbiVersionCode = project.ext.abiCodes.get(output.getFilter(OutputFile.ABI))
            output.versionCodeOverride = baseAbiVersionCode + variant.versionCode
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.arthenica:mobile-ffmpeg-full:4.2.1'
    // implementation project(':app')
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation 'com.android.support:support-v4:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
}
